plugins {
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless' version '8.0.0'
}

group = 'org.miracum.streams'
version = '3.0.9'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2025.0.0')
    set('hapiVersion', '8.4.0')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    implementation 'org.springframework.kafka:spring-kafka'

    implementation 'org.apache.kafka:kafka-streams'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'
    implementation 'io.micrometer:micrometer-core:1.16.0'

    implementation 'org.miracum:kafka-fhir-serializer:2.0.6'

    implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-caching-caffeine:${hapiVersion}"

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'

    implementation 'org.fhir:ucum:1.0.10'

    implementation platform('software.amazon.awssdk:bom:2.38.0')
    implementation 'software.amazon.awssdk:s3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jar {
    enabled = false
}

tasks.named('test') {
  useJUnitPlatform()
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = false
        csv.required = true
    }
}

jacoco {
    toolVersion = "0.8.14"
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
